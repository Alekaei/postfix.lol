OBTW
	Post Fix / Reverse Polish Notation
	Written in LOLCODE 1.4 

	WARNING
	=======
	Using future branch of lolcode
TLDR
HAI 1.4
	CAN HAS STDIO?
	CAN HAS STRING?

	OBTW
		Implementation of a stack
		Source: https://esolangs.org/wiki/LOLCODE
	TLDR
	O HAI IM PILE
		I HAS A LENGTH ITZ 0
		I HAS A MAX ITZ -1

		HOW IZ I PUSHIN YR ITEM
			DIFFRINT ME'Z MAX AN BIGGR OF ME'Z MAX AN ME'Z LENGTH, O RLY?
				YA RLY, ME HAS A SRS ME'Z LENGTH ITZ ITEM, ME'Z MAX R SUM OF ME'Z MAX AN 1
				NO WAI, ME'Z SRS ME'Z LENGTH R ITEM
			OIC
			ME'Z LENGTH R SUM OF ME'Z LENGTH AN 1
		IF U SAY SO

		HOW IZ I POPIN
			DIFFRINT ME'Z LENGTH AN 0, O RLY?
			YA RLY
				ME'Z LENGTH R DIFF OF ME'Z LENGTH AN 1
				I HAS A ITEM ITZ ME'Z SRS ME'Z LENGTH
				ME'Z SRS ME'Z LENGTH R NOOB
				FOUND YR ITEM
			OIC
		IF U SAY SO
	KTHX

	I HAS A STACK ITZ LIEK A PILE

	I HAS A POSTFIXSTATEMENT
	VISIBLE "Enter a postfix statement: "!
	GIMMEH POSTFIXSTATEMENT
	
	IM IN YR LOOP UPPIN YR INDEX TIL BOTH SAEM INDEX AN I IZ STRING'Z LEN YR POSTFIXSTATEMENT MKAY
		I HAS A CURCHAR ITZ I IZ STRING'Z AT YR POSTFIXSTATEMENT AN YR INDEX MKAY
		CURCHAR, WTF?
			OMG "+"
				I HAS A ARG1 ITZ STACK IZ POPIN MKAY
				I HAS A ARG2 ITZ STACK IZ POPIN MKAY
				STACK IZ PUSHIN YR SUM OF ARG2 AN ARG1 MKAY
				GTFO
			OMG "-"
				I HAS A ARG1 ITZ STACK IZ POPIN MKAY
				I HAS A ARG2 ITZ STACK IZ POPIN MKAY
				STACK IZ PUSHIN YR DIFF OF ARG2 AN ARG1 MKAY
				GTFO
			OMG "*"
				I HAS A ARG1 ITZ STACK IZ POPIN MKAY
				I HAS A ARG2 ITZ STACK IZ POPIN MKAY
				STACK IZ PUSHIN YR PRODUKT OF ARG2 AN ARG1 MKAY
				GTFO
			OMG "/"
				I HAS A ARG1 ITZ STACK IZ POPIN MKAY
				I HAS A ARG2 ITZ STACK IZ POPIN MKAY
				STACK IZ PUSHIN YR QUOSHUNT OF ARG2 AN ARG1 MKAY
				GTFO
			OMG " "
				BTW Skip empty chars
				GTFO
			OMGWTF
				BTW Cast the current character to a number and push it to the stack
				I HAS A CURNUM ITZ CURCHAR
				CURNUM IS NOW A NUMBAR BTW NUMBAR is float NUMBR is int
				STACK IZ PUSHIN YR CURNUM MKAY
		OIC
	IM OUTTA YR LOOP
	BTW Print the value at the end of the stack
	VISIBLE STACK IZ POPIN MKAY
KTHXBYE
